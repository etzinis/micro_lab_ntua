IN 10H


START: 	CALL ROUTINE    ;PROGRAMM TO CALL
		JMP START       ;THE ROUTINE

ROUTINE:	PUSH PSW        ;SAVE REGS
		PUSH B
		PUSH H
		PUSH D
		
		LXI H,0A00H
		MVI M,10H
		INX H
		MVI M,10H   
		INX H
		MVI M,10H   
		INX H
		MVI M,10H   
		INX H
		MVI M,10H	   ;NONE OUTPUT IS SHOWN BY 10H
		INX H
		MVI M,10H        ;INITIALIZE OUTPUT TO NONE
		
		LDA 2000H        ;Read the input
		MOV D,A	   ;SAVE INPUT IN D
		RAL		   ;CHECK MSB IF NUMBER IS NEG
		JNC POSITIVE	   ;IF MSB IS 0 THEN POSITIVE

NEGATIVE:	MOV A,D          ;PARE PALI THN EISODO
		CMA              ;COMPLEMENT OF 1
		ADI 01H          ;COMPLEMENT OF 2
		MOV D,A	   ;SAVE RIGHT OUTPUT
		MVI A,1CH        ;A= CODE OF "-"
		STA 0A03H	   ;PUT IT IN THE 4TH DISPLAY
		JMP SIGNOK

POSITIVE:     MVI A,10H        ;A= CODE OF " " NONE OUTPUT
		STA 0A03H	    ;PUT IT IN THE 4TH DISPLAY

SIGNOK:	MOV A,D	    ;NOW WE HAVE TO SHOW THE NUMBER
		CPI 64H           ;NUM < 100 => CARRY BECOMES 1
		JC LESS100

GR100:   	MVI A,01H        ;CODE OF 1 dec IN HUNDREADS
		STA 0A02H        ;SHOW IT
		MOV A,D	   ;TAKE THE NUMBER AGAIN
		SUI 64H          ;NUMBER = NUMBER - 100
		MOV D,A 	   ;SAVE THE NUMBER
		JMP HUNOK

LESS100: 	MVI A,00H        ;CODE OF 0 dec IN HUNDREADS
		STA 0A02H        ;SHOW IT

HUNOK:	       MOV A,D          ;NOW WE CARE ONLY FOR DEC AND UNITS
		MVI B,00H	   ;DECADES = 0
FINDDEC:	CPI 0AH 	   ;IF NUM < 10 THEN CARRY BECOMES 1
		JC DECUNITSOK
		
		INR B	          ;DEC ++
		SUI 0AH          ;NUM = NUM -10
		JMP FINDDEC

DECUNITSOK: 
		MOV D,A	   ;A HAS THE UNITS NOW		
		MOV A,B	   ;A=DECADES
		STA 0A01H	   ;SHOW TO THE RIGHT PLACE
		MOV A,D	   ;TAKE THE UNITS
		STA 0A00H	   ;SHOW IT
		
		LXI D,0A00H
		CALL STDM	   ;USE THE DISPLAY ROUTINES
		CALL DCD
		
		POP D	          ;RETURN THE VAUES OF
		POP H	          ;ALL THE REGISTERS
		POP B
		POP PSW
		RET
		
END
		
		
		
